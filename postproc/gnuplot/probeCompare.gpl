#!/usr/bin/env gnuplot
#
# Purpose:  Plot temporal evolution of azimuthal velocity at one probe position
#           for different Grashof numbers, output from different nsCouette
#           simultions, nsCouette/tc0073, tc0074, tc0075
# Usage:    gnuplot probeCompare.gpl
# Authors:  Daniel Feldmann
# Created:  25th August 2018
# Modified: 19th March 2019

# output
set terminal postscript eps enhanced color font 'Times-Roman,16' linewidth 1.5 size 6.00cm, 05.00cm
file = 'probeCompare.eps'
set output file

# title
set title 'Temporal evolution of streamwise velocity'
set notitle 

# Taylor-Couette set-up
Re_i = 50.0 # inner cylinder Reynolds number
Re_o =  0.0 # outer cylinder Reynolds number
eta = 0.500 # raddii ratio

# Analyitcal Taylor-Couette solution
nutc = 1.0 # Taylor-Couette analytical torque (Nusselt number)
c1 = (Re_o - eta * Re_i) / (1 + eta)
c2 = (eta * (Re_i - eta*Re_o)) / ((1 - eta) * (1 - eta**2.0))
r2 = 1.52532458441936 # radial probe location from file header
utc2 = c1*r2 + c2/r2  # Taylor-Couette analytical velocity

# legend
set key top left
unset key

# 1st x axis
set xlabel 't in d^2/{/Symbol n}'
set format x "%.0f"
set xrange [0.0: 12.0]
set xtics   0.0, 4.0
set mxtics  4

# 1st y axis
set ylabel 'u_{/Symbol q}'
set format y "%.0f"
set yrange [-30:  60]
set ytics   -30,  30
set mytics   3

# grid lines
set nogrid

# line styles with colours appropriate for colour-blind
set style line 10 lc rgb '#000000' dt 1 lw 1.5 # black
set style line 11 lc rgb '#D55E00' dt 1 lw 1.5 # vermillon
set style line 12 lc rgb '#0072B2' dt 1 lw 1.5 # blue
set style line 13 lc rgb '#009E73' dt 1 lw 1.5 # bluish green
set style line 14 lc rgb '#E69F00' dt 1 lw 1.5 # orange
set style line 15 lc rgb '#56B4E9' dt 1 lw 1.5 # sky blue
set style line 16 lc rgb '#CC79A7' dt 1 lw 1.5 # reddish purple
set style line 17 lc rgb '#F0E442' dt 1 lw 1.5 # yellow 

# main plot
set style data lines
plot utc2 t 'Analytical' ls 10,\
     '../tc0073fsm/probe02.dat' u ($1):($3) t 'Gr = 3000' ls 13,\
     '../tc0075fsm/probe02.dat' u ($1):($3) t 'Gr = 4000' ls 11,\
     '../tc0076fsm/probe02.dat' u ($1):($3) t 'Gr = 5000' ls 12

# convert to pdf (epstopdf comes with e.g. TeXlive)
system('epstopdf '.file)
