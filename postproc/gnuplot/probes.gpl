#!/usr/bin/env gnuplot
#
# Purpose:  Plot temporal evolution of velocity at several probe locations based
#           on time series output from a nsCouette simulation, nsCouette/tc0040
# Usage:    gnuplot torque.gpl
# Authors:  Daniel Feldmann
# Created:  25th August 2018
# Modified: 11th October 2018

# output
set terminal postscript eps enhanced color font 'Times-Roman,16' linewidth 1.5 size 14.50cm, 06.00cm
file = 'probes.eps'
set output file

# title
set title 'Temporal evolution of velocity at several probe locations, tc0042'
set notitle 

# Taylor-Couette set-up
Re_i = 458.1 # inner cylinder Reynolds number
Re_o =  0.0 # outer cylinder Reynolds number
eta = 0.868 # raddii ratio

# Analyitcal Taylor-Couette solution
nutc = 1.0 # Taylor-Couette analytical torque (Nusselt number)
c1 = (Re_o - eta * Re_i) / (1 + eta)
c2 = (eta * (Re_i - eta*Re_o)) / ((1 - eta) * (1 - eta**2.0))
r1 = 6.769704584483744 # radial probe location from file header
r2 = 7.101082160176931 # radial probe location from file header
r3 = 7.381810567031406 # radial probe location from file header
utc1 = c1*r1 + c2/r1  # Taylor-Couette analytical velocity
utc2 = c1*r2 + c2/r2  # Taylor-Couette analytical velocity
utc3 = c1*r3 + c2/r3  # Taylor-Couette analytical velocity

# legend
set key bottom right

# 1st x axis
set xlabel 't in d^2/{/Symbol n}'
set format x "%.1f"
set xrange [0.0: 2.0]
set xtics   0.0, 0.5
set mxtics  5

# grid lines
set nogrid

# line styles with colours appropriate for colour-blind
set style line 10 lc rgb '#000000' dt 2 lw 1.5 # black
set style line 11 lc rgb '#D55E00' dt 1 lw 1.5 # vermillon
set style line 12 lc rgb '#0072B2' dt 1 lw 1.5 # blue
set style line 13 lc rgb '#009E73' dt 1 lw 1.5 # bluish green
set style line 14 lc rgb '#E69F00' dt 1 lw 1.5 # orange
set style line 15 lc rgb '#56B4E9' dt 1 lw 1.5 # sky blue
set style line 16 lc rgb '#CC79A7' dt 1 lw 1.5 # reddish purple
set style line 17 lc rgb '#F0E442' dt 1 lw 1.5 # yellow 

# main plot
set style data lines
set multiplot layout 1,2

set key top right
set ylabel 'u_{/Symbol q} in d/{/Symbol n}'
set yrange [50.0: 400.0]
set ytics    0.0, 100.0
set mytics   2
plot utc1 t 'Taylor-Couette analytical solution' ls 10,\
     utc2 t ''                                   ls 10,\
     utc3 t ''                                   ls 10,\
     'probe01.dat' u ($1):($3) t 'r = 6.8' ls 11,\
     'probe02.dat' u ($1):($3) t 'r = 7.1' ls 12,\
     'probe03.dat' u ($1):($3) t 'r = 7.4' ls 13

set key bottom left
set ylabel '|u_{/Symbol a}| in d/{/Symbol n}'
set yrange [-30: 15]
set ytics -30, 15
set mytics 3 
#set format y "10^{%.1T}"
plot 'probe02.dat' u ($1):(($2)) t '{/Symbol a} = r' ls 11,\
     'probe02.dat' u ($1):(($4)) t '{/Symbol a} = z' ls 12

unset multiplot

# convert to pdf (epstopdf comes with e.g. TeXlive)
system('epstopdf '.file)
