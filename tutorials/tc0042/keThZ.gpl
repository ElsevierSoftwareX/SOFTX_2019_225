#!/usr/bin/env gnuplot
#
# Purpose:  Plot temporal evolution of azimuthallly/axially dependenet kinetic
#           energy for some selected azimuthal/axial modes based on time series
#           output from a nsCouette simulation, nsCouette/tc0042
# Usage:    gnuplot keThZ.gpl
# Authors:  Daniel Feldmann
# Created:  25th August 2018
# Modified: 11th Ocotber 2018

# output
set terminal postscript eps enhanced color font 'Times-Roman,16' linewidth 1.5 size 14.50cm, 06.00cm
file = 'keThZ.eps'
set output file

# title
set title 'Temporal evolution of azimuthally/axially dependent kinetic energy, tc0042'
set notitle 

# 1st x axis
set xlabel 't in d^2/{/Symbol n}'
set format x "%.1f"
set xrange [0.0: 2.0]
set xtics   0.0, 0.5
set mxtics  5

# grid lines
set nogrid

# line styles with colours appropriate for colour-blind
set style line 10 lc rgb '#000000' dt 1 lw 1.5 # black
set style line 11 lc rgb '#D55E00' dt 1 lw 1.5 # vermillon
set style line 12 lc rgb '#0072B2' dt 1 lw 1.5 # blue
set style line 13 lc rgb '#009E73' dt 1 lw 1.5 # bluish green
set style line 14 lc rgb '#E69F00' dt 1 lw 1.5 # orange
set style line 15 lc rgb '#56B4E9' dt 1 lw 1.5 # sky blue
set style line 16 lc rgb '#CC79A7' dt 1 lw 1.5 # reddish purple
set style line 17 lc rgb '#F0E442' dt 1 lw 1.5 # yellow 
set style line 18 lc 'grey'        dt 1 lw 1.5 # grey

# main plot
set style data lines
set multiplot layout 1,2

# sub-plot 1
set key bottom right 
set ylabel 'E_{/Symbol q} in d^2/{/Symbol n}^2'
set logscale y
set format y "10^{%+3.1T}"
set yrange [1.0e-37: 1.0e+05]
set ytics   1.0e-40, 1.0e+10, 1.0e+10
unset mytics 
plot 'ke_th' u ($1):($2)  t 'n_{/Symbol q} = 0' ls 10,\
     'ke_th' u ($1):($3)  t 'n_{/Symbol q} = 1' ls 11,\
     'ke_th' u ($1):($4)  t ''                  ls 18,\
     'ke_th' u ($1):($5)  t ''                  ls 18,\
     'ke_th' u ($1):($6)  t ''                  ls 18,\
     'ke_th' u ($1):($7)  t ''                  ls 18,\
     'ke_th' u ($1):($8)  t 'n_{/Symbol q} = 6' ls 12,\
     'ke_th' u ($1):($9)  t ''                  ls 18,\
     'ke_th' u ($1):($10) t ''                  ls 18,\
     'ke_th' u ($1):($11) t ''                  ls 18,\
     'ke_th' u ($1):($12) t ''                  ls 18,\
     'ke_th' u ($1):($13) t 'n_{/Symbol q} =11' ls 13,\
     'ke_th' u ($1):($14) t ''                  ls 18,\
     'ke_th' u ($1):($15) t ''                  ls 18,\
     'ke_th' u ($1):($16) t ''                  ls 18,\
     'ke_th' u ($1):($17) t ''                  ls 18,\
     'ke_th' u ($1):($18) t 'n_{/Symbol q} =16' ls 14

# sub-plot 2
set key bottom left
set ylabel 'E_{z} in d^2/{/Symbol n}^2'
#set yrange [1.0e-21: 1.0e+05]
plot 'ke_z' u ($1):($2)  t 'l_{z} = 0' ls 10,\
     'ke_z' u ($1):($3)  t 'l_{z} = 1' ls 11,\
     'ke_z' u ($1):($4)  t ''          ls 18,\
     'ke_z' u ($1):($5)  t ''          ls 18,\
     'ke_z' u ($1):($6)  t ''          ls 18,\
     'ke_z' u ($1):($7)  t ''          ls 18,\
     'ke_z' u ($1):($8)  t 'l_{z} = 6' ls 12,\
     'ke_z' u ($1):($9)  t ''          ls 18,\
     'ke_z' u ($1):($10) t ''          ls 18,\
     'ke_z' u ($1):($11) t ''          ls 18,\
     'ke_z' u ($1):($12) t ''          ls 18,\
     'ke_z' u ($1):($13) t 'l_{z} =11' ls 13,\
     'ke_z' u ($1):($14) t ''          ls 18,\
     'ke_z' u ($1):($15) t ''          ls 18,\
     'ke_z' u ($1):($16) t ''          ls 18,\
     'ke_z' u ($1):($17) t ''          ls 18,\
     'ke_z' u ($1):($18) t 'l_{z} =16' ls 14

unset multiplot

# convert to pdf (epstopdf comes with e.g. TeXlive)
system('epstopdf '.file)
